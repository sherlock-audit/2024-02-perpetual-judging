Massive Blush Wasp

medium

# Settle order on `OrderGatewayV2` will fail when margin to withdraw is so small it rounds down to zero

## Summary

When settling an order via `OrderGatewayV2`, the final margin to withdraw it may be so small that it rounds down to zero, reverting the whole transaction. 

## Vulnerability Detail

The contract `OrderGatewayV2` is responsible for settling positions between them and it works in the following way:

1. Fills the taker order
2. Fills the maker order (in case is not whitelisted)
3. Settles the position on `ClearingHouse`
4. If the position is reducing, withdraw some margin from the trader to keep the same leverage

The issue here is in point 4, the step where the contract withdraws some margin from the trader in case its position has been reduced. This step is done to maintain the same leverage on the trader account after reducing a position. 

This last step is done here:

https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/orderGatewayV2/OrderGatewayV2.sol#L469-L475

```solidity
    function _withdrawMargin(
        InternalContext memory context,
        uint256 marketId,
        address trader,
        uint256 requiredMarginRatio
    ) internal {
        uint256 withdrawableMargin = _getWithdrawableMargin(context, marketId, trader, requiredMarginRatio);
>>      if (withdrawableMargin > 0) {
            context.vault.transferMarginToFund(
                marketId,
                trader,
                withdrawableMargin.formatDecimals(INTERNAL_DECIMALS, context.collateralTokenDecimals)
            );
        }
    }
```

However, there's an issue here that will cause the whole transaction to revert in certain scenarios, leading to that position not being settled. 

In the scenario where the value of `withdrawableMargin` is small enough, when converting its decimals to the decimals of the collateral token (usually 6 decimals), the value will be rounded down to zero. When we call `vault.transferMarginToFund` with a value of zero, the transaction will revert:

https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/vault/Vault.sol#L412
```solidity
    function transferMarginToFund(uint256 marketId, address trader, uint256 amountXCD) external nonReentrant {
        _checkIsSenderAuthorized(trader);
        _transferMarginToFund(marketId, trader, amountXCD);
    }

    function _transferMarginToFund(
        uint256 marketId,
        address trader,
        uint256 amountXCD
    ) internal marketExistsAndActive(marketId) {
        if (amountXCD == 0) {
>>          revert LibError.ZeroAmount();
        }

        // ...
    }
```

The actual decimal conversion is done in the function `formatDecimals`:

```solidity
function formatDecimals(uint256 num, uint8 fromDecimals, uint8 toDecimals) internal pure returns (uint256) {
    if (fromDecimals == toDecimals) {
        return num;
    }
    return
        fromDecimals >= toDecimals
            ? num / 10 ** (fromDecimals - toDecimals)
            : num * 10 ** (toDecimals - fromDecimals);
}
```

For example, when the value of `withdrawableMargin` is 900000000000 represented in 18 decimals (0.0000009e18), when converting it to 6 decimals (collateral decimals) the result will be 0, thus reverting the transaction. 

## Impact

When settling an order via `OrderGatewayV2`, the final margin to withdraw it may be so small that it rounds down to zero, reverting the whole transaction and preventing that order from being settled. This DoS can be extended until the price changes enough to make the margin to withdraw bigger or to make it zero. 

Given that the profit or loss of a position can vary depending on the current price, not settling an order on time will cause losses for users in certain scenarios where the price moves against their positions during that DoS period. 

## Code Snippet

https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/orderGatewayV2/OrderGatewayV2.sol#L469-L475

## Tool used

Manual Review

## Recommendation

To mitigate this issue, is recommended to check if the value of `withdrawableMargin` converted to collateral decimals is zero before withdrawing the margin:

```diff
    function _withdrawMargin(
        InternalContext memory context,
        uint256 marketId,
        address trader,
        uint256 requiredMarginRatio
    ) internal {
        uint256 withdrawableMargin = _getWithdrawableMargin(context, marketId, trader, requiredMarginRatio);
+       uint256 withdrawableMarginXCD = withdrawableMargin.formatDecimals(INTERNAL_DECIMALS, context.collateralTokenDecimals);
        
-       if (withdrawableMargin > 0) {
+       if (withdrawableMarginXCD > 0) { 
            context.vault.transferMarginToFund(
                marketId,
                trader,
-               withdrawableMargin.formatDecimals(INTERNAL_DECIMALS, context.collateralTokenDecimals)
+               withdrawableMarginXCD
            );
        }
    }
```